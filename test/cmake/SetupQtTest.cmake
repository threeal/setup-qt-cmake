include(SetupQt)

function("Download Qt online installer")
  _download_qt_online_installer()

  if(CMAKE_SYSTEM_NAME STREQUAL Darwin)
    if(NOT DEFINED QT_ONLINE_INSTALLER_IMAGE)
      message(FATAL_ERROR "The 'QT_ONLINE_INSTALLER_IMAGE' variable should be defined")
    elseif(NOT EXISTS "${QT_ONLINE_INSTALLER_IMAGE}")
      message(FATAL_ERROR "The downloaded installer image should exist at '${QT_ONLINE_INSTALLER_IMAGE}'")
    elseif(DEFINED QT_ONLINE_INSTALLER_PROGRAM)
      message(FATAL_ERROR "The 'QT_ONLINE_INSTALLER_PROGRAM' variable should not be defined")
    endif()
  else()
    if(NOT DEFINED QT_ONLINE_INSTALLER_PROGRAM)
      message(FATAL_ERROR "The 'QT_ONLINE_INSTALLER_PROGRAM' variable should be defined")
    elseif(NOT EXISTS "${QT_ONLINE_INSTALLER_PROGRAM}")
      message(FATAL_ERROR "The downloaded installer program should exist at '${QT_ONLINE_INSTALLER_PROGRAM}'")
    elseif(DEFINED QT_ONLINE_INSTALLER_IMAGE)
      message(FATAL_ERROR "The 'QT_ONLINE_INSTALLER_IMAGE' variable should not be defined")
    endif()
  endif()
endfunction()

function("Attach and detach Qt online installer")
  _download_qt_online_installer()
  _attach_qt_online_installer()

  if(CMAKE_SYSTEM_NAME STREQUAL Darwin)
    if(NOT DEFINED QT_ONLINE_INSTALLER_VOLUME)
      message(FATAL_ERROR "The 'QT_ONLINE_INSTALLER_VOLUME' variable should be defined")
    elseif(NOT EXISTS "${QT_ONLINE_INSTALLER_VOLUME}")
      message(FATAL_ERROR "The attached installer should exist at '${QT_ONLINE_INSTALLER_VOLUME}'")
    endif()
  else()
    if(DEFINED QT_ONLINE_INSTALLER_VOLUME)
      message(FATAL_ERROR "The 'QT_ONLINE_INSTALLER_VOLUME' variable should not be defined")
    endif()
  endif()

  if(NOT DEFINED QT_ONLINE_INSTALLER_PROGRAM)
    message(FATAL_ERROR "The 'QT_ONLINE_INSTALLER_PROGRAM' variable should be defined")
  elseif(NOT EXISTS "${QT_ONLINE_INSTALLER_PROGRAM}")
    message(FATAL_ERROR "The installer program should exist at '${QT_ONLINE_INSTALLER_PROGRAM}'")
  endif()

  _detach_qt_online_installer()

  if(CMAKE_SYSTEM_NAME STREQUAL Darwin)
    if(DEFINED QT_ONLINE_INSTALLER_VOLUME)
      message(FATAL_ERROR "The 'QT_ONLINE_INSTALLER_VOLUME' variable should not be defined")
    elseif(DEFINED QT_ONLINE_INSTALLER_PROGRAM)
      message(FATAL_ERROR "The 'QT_ONLINE_INSTALLER_PROGRAM' variable should not be defined")
    endif()
  else()
    if(NOT DEFINED QT_ONLINE_INSTALLER_PROGRAM)
      message(FATAL_ERROR "The 'QT_ONLINE_INSTALLER_PROGRAM' variable should be defined")
    endif()
  endif()
endfunction()

function("Execute Qt online installer")
  _download_qt_online_installer()
  if(DEFINED QT_ONLINE_INSTALLER_IMAGE)
    _attach_qt_online_installer()
  endif()

  if(NOT DEFINED QT_ONLINE_INSTALLER_PROGRAM)
    message(FATAL_ERROR "The 'QT_ONLINE_INSTALLER_PROGRAM' variable should be defined")
  elseif(NOT EXISTS "${QT_ONLINE_INSTALLER_PROGRAM}")
    message(FATAL_ERROR "The installer program should exist at '${QT_ONLINE_INSTALLER_PROGRAM}'")
  endif()

  execute_process(
    COMMAND "${QT_ONLINE_INSTALLER_PROGRAM}" --version
    RESULT_VARIABLE RES
  )
  if(NOT RES EQUAL 0)
    message(FATAL_ERROR "Should not fail to execute the installer program: ${RES}")
  endif()

  if(DEFINED QT_ONLINE_INSTALLER_VOLUME)
    _detach_qt_online_installer()
  endif()
endfunction()

if(NOT DEFINED TEST_COMMAND)
  message(FATAL_ERROR "The 'TEST_COMMAND' variable should be defined")
elseif(NOT COMMAND "${TEST_COMMAND}")
  message(FATAL_ERROR "Unable to find a command named '${TEST_COMMAND}'")
endif()

cmake_language(CALL "${TEST_COMMAND}")
